
/* A classe recebe um valor do teclado e realiza os eventos de cada componente do jogo*/

class Game { 

	field Tank p1, p2; 

	constructor Game new() { 
		let p1 = Tank.new(10, 10, 0); 
		let p2 = Tank.new(200, 200, 0); 
		return this; 
	}

	method void run() { 
		var char key; 
		var boolean exit;  
		var Bullet b; 
		var boolean over1;
		var boolean over2;
		

		let exit = false; 
		let over1 = false; 
		let over2 = false;

		while (true) { 
			while (key = 0) { 
				let key = Keyboard.keyPressed(); 
				do moveBullets(p1);
				do moveBullets(p2);
				let over1 = (checkHit(p1, p2));
				let over2 = (checkHit(p2, p1)); 
				if (over1) { 
					do Output.printString("Player 2 WIN");
					do Sys.halt();
				}
				if (over2){
					do Output.printString("Player 1 WIN");
					do Sys.halt();
				}
				do moveTanks(p1); 
				do moveTanks(p2); 
			}

			/*TECLA P PARA O PLAYER 1 ATIRAR*/
			if (key = 80) { 
				let b = p1.getBullet(); 
				if (b.getSpeed() = 0) { 
					do p1.fireBullet(); 
				}
			}

			/*TECLA ESPAÃ‡O PARA O PLAYER 2 ATIRAR*/
			if (key = 32) { 
				let b = p2.getBullet(); 
				if (b.getSpeed() = 0) { 
					do p2.fireBullet(); 
				}
			}

			/* SETAS PARA JOGADOR 1 MOVIMENTAR*/ 
			if (key = 131) { 
				do p1.setDir(1); 
			}
			if (key = 133) { 
				do p1.setDir(2); 
			}
			if (key = 130) { 
				do p1.setDir(3); 
			}
			if (key = 132) { 
				do p1.setDir(4); 
			}

			/* AWSD PARA JOGADOR 2 MOVIMENTAR*/
			if (key = 87) { 
				do p2.setDir(1); 
			}
			if (key = 83) { 
				do p2.setDir(2); 
			}
			if (key = 65) { 
				do p2.setDir(3); 
			}
			if (key = 68) { 
				do p2.setDir(4); 
			}
			while (~(key = 0)) { 
				let key = Keyboard.keyPressed(); 
				do moveBullets(p1); 
				do moveBullets(p2); 
				let over1 = (checkHit(p1, p2));
				let over2 = (checkHit(p2, p1)); 
				if (over1) { 
					do Output.printString("Player 2 WIN");
					do Sys.halt();
				}
				if (over2){
					do Output.printString("Player 1 WIN");
					do Sys.halt();
				}
				do moveTanks(p1); 
				do moveTanks(p2); 
			}
		}
		return; 
	}

	 
	/* verifica se a bala do jogador p2 acertou p1*/
	method boolean checkHit(Tank p1, Tank p2) { 
		var Bullet b; 
		let b = p2.getBullet(); 
		if (b.getSpeed() > 0) { 
			if ((b.getRow() > p1.getRow()) & 
					(b.getRow() < (p1.getRow() + p1.getLength())) &
					(b.getCol() > p1.getCol()) &
					(b.getCol() < (p1.getCol() + p1.getLength()))) {	
				return true; 
			}
		}
		return false; 
	}

	/* Move o tanque */ 
	method void moveTanks(Tank t) { 
		if (t.getDir() = 1) { 
			do t.moveUp(); 
		}
		if (t.getDir() = 2) { 
			do t.moveDown(); 
		}
		if (t.getDir() = 3) { 
			do t.moveLeft(); 
		}
		if (t.getDir() = 4) { 
			do t.moveRight(); 
		}
		do Sys.wait(4); 
		return; 
	}

	/* Move a bala caso ela tenha sido disparada */ 
	method void moveBullets(Tank t) { 
		var Bullet b; 
		let b = t.getBullet(); 

		if (b.getSpeed() > 0) { 
			if (b.getDir() = 1) { 
				do b.moveUp(); 
			}
			if (b.getDir() = 2) { 
				do b.moveDown(); 
			}
			if (b.getDir() = 3) { 
				do b.moveLeft(); 
			}
			if (b.getDir() = 4) { 
				do b.moveRight(); 
			}
		}
		do Sys.wait(4); 
		return; 
	}
}
